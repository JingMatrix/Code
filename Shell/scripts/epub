#! zsh
# If provide hash from reader.epubee.com, then download that e-book, otherwise parse a given file

tidy_config=$(dirname $0)/../../Web/tidy.config

function htmlparser {
	readerhtml="=['\"]\/books\/mobile\/|^<meta |<\/div><\/div><\/div>"
	readercontainer='^<div class="readercontent"><div class="readercontent-inner">'
	if grep -q $readercontainer $1; then
		sed <$1 -n -E "/$readerhtml/d;1,/<\/head>/p;/$readercontainer/,\$p" |
			sed -E "s#$readercontainer#<body><div>#;s#^</body>#</div></body>#" |
			fixhtml
	else
		echo >&2 "No epubee reader fingerprint found"
		fixhtml <$1
	fi
}

function fixhtml {
	if [[ -f $tidy_config ]]; then
		tidy -config $tidy_config -o |
			sed '3s#""#"http://www.w3.org/TR/xhtml11/DTD/xhtml11.dtd"#'
		# fix doctype
	else
		echo $tidy_config not exists
	fi
}

function parseepubee {
	cd $rootdir-bak
	# parse html files
	for html in OEBPS/**html; do
		htmlparser $rootdir-bak/$html >$rootdir/$html
	done
	# parse toc.ncx
	if [[ -f OEBPS/toc.ncx ]]; then
		sed -i '/ content="bookid"/d' $rootdir/OEBPS/toc.ncx
	fi
	# parse opf
}

function packepub {
	cd $1
	zip -rX $2 *
	(sigil $2 &)
}

function checkepub {
	epubcheck $rootdir --mode exp --save --json $rootdir.json
	jq '.messages | .[] | .message' $rootdir.json -r
	jq '.messages | .[] | .message, (.locations | .[] | [.path, .line, .column] | @tsv)' $rootdir.json -r >$rootdir.error
	errorfilelist=$(jq '.messages | .[] | .locations | .[] | .path' $rootdir.json -r | uniq | tr "\n" " ")
	filename="/tmp/$(jq '.publication | .title' $rootdir.json -r).epub"
	if [[ -f $rootdir.epub ]]; then
		mv $rootdir.epub $filename
	else
		repairepub
	fi
}

function repairepub {
	echo -n "Edit with vim?\t"
	# loop when edit with vim
	while read -q; do
		[[ -f $rootdir.json ]] || checkepub
		cd $rootdir
		tmux split-window -b "vim $errorfilelist"
		less $rootdir.error
		echo -n "\nRun epubcheck again?\t"
		rm $rootdir.json
	done

	echo -n "Change to html directory?\t"
	read -q && cd $rootdir/OEBPS && $SHELL
	echo -n "Pack epub directly?\t"
	read -q && packepub $rootdir $filename && less $rootdir.error
}

epubhash=$(grep -oP '[0-9a-f]{32}' <<<$1)
if [[ $epubhash && -z $2 ]]; then
	# download files from epubee.com
	rootdir=/tmp/epub/$epubhash
	if [[ -d $rootdir-bak ]]; then
		echo -n "Use cached download files, parser them again?\t"
		if read -q; then
			parseepubee
		fi
	else
		mkdir -p $rootdir/
		mkdir -p $rootdir/OEBPS
		mkdir -p $rootdir/META-INF
		cd $rootdir/
		echo -n 'application/epub+zip' >mimetype
		echo -n '<?xml version="1.0" encoding="UTF-8" ?>
<container version="1.0" xmlns="urn:oasis:names:tc:opendocument:xmlns:container">
  <rootfiles>
    <rootfile full-path="OEBPS/content.opf" media-type="application/oebps-package+xml"/>
  </rootfiles>
</container>' >META-INF/container.xml
		cd $rootdir/OEBPS
		root=http://reader.epubee.com/books/mobile/$epubhash[0,2]/$epubhash
		aria2c $root/content.opf
		grep -oP 'item href="\K[^"]+' content.opf | while read -r file; do
			dir=$(dirname $file)
			[[ -d $dir ]] || mkdir -p $dir
			aria2c $root/$file -d $dir
		done
		cp $rootdir $rootdir-bak -r
	fi
	# parse htmls only if haven't
	[[ ! -f $rootdir.json ]] && parseepubee

	# pack epub file and start edit if not well format

	filename="/tmp/$(grep -oP '<dc:title>\K[^<]+' $rootdir/OEBPS/content.opf).epub"
	[[ -f $rootdir.error ]] && echo -n "Skip epubcheck? Directly pack epub\t"
	if read -q; then
		packepub $rootdir $filename
	else
		checkepub
	fi
else
	if [[ -f $1 ]]; then
		# parse given file in argument
		echo parse html file $1
		htmlparser $1
	elif [[ -d $1 ]]; then
		# pack directory as epub
		[[ -z $2 ]] && 2="/tmp/$(basename $1).epub"
		packepub $1 $2
	fi
fi
